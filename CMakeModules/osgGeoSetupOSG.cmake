# Sets up the following variables
#	OSGGEO_OSG_LIBRARY
#	OSGGEO_OSGDB_LIBRARY
#	OSGGEO_OSGGA_LIBRARY
#	OSGGEO_OSGUTIL_LIBRARY
#	OSGGEO_OSGMANIPULATOR_LIBRARY
#	OSGGEO_OSGWIDGET_LIBRARY
#	OSGGEO_OSGVIEWER_LIBRARY
#	OSGGEO_OSGVOLUME_LIBRARY
#	OSGGEO_OSGTEXT_LIBRARY
#	OSGGEO_OSGSIM_LIBRARY
#	OSGGEO_OPENTHREADS_LIBRARY
#
# Written by: Kristofer Tingdahl

set(OSG_DIR "" CACHE PATH REQUIRED)
set(OSGGEO_USE_DEBUG_OSG "" CACHE BOOL REQUIRED)

set (CMAKE_DEBUG_POSTFIX d)
list(APPEND CMAKE_MODULE_PATH ${osgGeo_SOURCE_DIR}/CMakeModules)
if(POLICY CMP0017)
    cmake_policy(SET CMP0017 NEW)
endif()

find_package( OpenSceneGraph QUIET COMPONENTS osgDB osgGA osgUtil osgManipulator osgWidget osgViewer osgVolume osgText osgSim )

if ( OSGGEO_USE_DEBUG_OSG )
    set(OSGGEO_OSG_LIBRARY_POSTIFIX "_DEBUG" )
else()
    set(OSGGEO_OSG_LIBRARY_POSTIFIX )
endif()

set ( OSGGEO_OSG_MODULES
	OSG
	OSGDB
	OSGGA
	OSGUTIL
	OSGMANIPULATOR
	OSGWIDGET
	OSGVIEWER
	OSGVOLUME
	OSGTEXT
	OSGSIM
	OPENTHREADS )

foreach( OSGMODULE ${OSGGEO_OSG_MODULES} )
    if ( "${CMAKE_BUILD_TYPE}" STREQUAL "Release" )
	if( ${OSGMODULE}_LIBRARY )
	    set ( CUROSGLIB ${${OSGMODULE}_LIBRARY} )
	elseif( ${OSGMODULE}_LIBRARY_DEBUG )
	    set ( CUROSGLIB ${${OSGMODULE}_LIBRARY_DEBUG} )
	else()
	    message( FATAL_ERROR "${OSGMODULE}_LIBRARY is missing" )
	endif()
    else()
	if( ${OSGMODULE}_LIBRARY_DEBUG )
	    set ( CUROSGLIB ${${OSGMODULE}_LIBRARY_DEBUG} )
	elseif( ${OSGMODULE}_LIBRARY )
	    set ( CUROSGLIB ${${OSGMODULE}_LIBRARY} )
	else()
	    message( FATAL_ERROR "${OSGMODULE}_LIBRARY is missing" )
	endif()
    endif()

    if( CUROSGLIB )
	set( OSGGEO_${OSGMODULE}_LIBRARY ${CUROSGLIB} )
    endif()
endforeach()
